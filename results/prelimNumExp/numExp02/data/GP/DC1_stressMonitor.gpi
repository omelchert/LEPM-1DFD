set terminal postscript enhanced eps size 9.5cm,9.5cm color lw 1 16 font "Times-Roman"
set output './FIGS/transducerSignal_backinglayer.eps'

LW=1.5
set style line 1 lw LW lc rgb 'blue'
set style line 2 lw LW lc rgb 'dark-grey' dashtype 2 

unset key
set tics nomirror
set lmargin at screen 0.11 
set rmargin at screen 0.98
set xzeroaxis

set label 11 '(a)' at screen 0.025,0.98 center 
set label 12 '(b)' at screen 0.025,0.63 center 

set xr [0.2:1.1]
set xtics (0.,0.2,0.4,0.6,0.8,1.0)

set multiplot

set label 1 '{/Times-Italic U(t)} (arb. units)' rotate by 90 at screen 0.06,0.98 right 
set yr [-0.002:0.002]
set ytics ('' -0.002, '' -0.0015, '' -0.001,'' -0.0005,'' 0.,'' 0.0005,'' 0.001,'' 0.0015,'' 0.002)
set tmargin at screen 0.98
set bmargin at screen 0.65
set format x ''
p '../transducerSignal_DC1.dat' u 1:2 w l ls 1 t 'DC-1'

unset label 1
unset label 11 
unset label 12


set bmargin at screen 0.07
set tmargin at screen 0.63
set yr [-3.0:1.5]
set ytics (-3,-0.25,0.,0.5) #rotate by 20
set format y "%3.2f"
set format x "%2.1f"
set label 2 '{/Times-Italic t} ({/Symbol m}s)' at screen 0.98,0.03 right 
set label 3 '{/Times-Italic z} (mm)' rotate by 90 at screen 0.06,0.63 right 
set tics out 
set arrow 1 nohead lw 3 dashtype 2 from 0.2, 0.00 to 1.2, 0.00 front
set arrow 2 nohead lw 3 dashtype 2 from 0.2,-0.25 to 1.2,-0.25 front
set label 4 '{/Times-Italic p(z,t)}' at screen 0.15,0.61 front

#set palette defined (0 'red', 0.5 'white', 1 'blue')
set palette defined (0 'dark-pink', 0.5 'light-grey', 1 'blue')

p '../DC1_propagatingStressProfile.dat' nonuniform matrix u 2:($1*10):3 with image

unset multiplot
