set terminal postscript enhanced eps size 9.5cm,9.5cm color lw 1 16 font "Times-Roman"
set output './FIGS/stressMonitor_pzt.eps'

set label 106 "{/Times-Roman=150 DRAFT}" at screen 0.5,0.5 center rotate by 25 tc "dark-grey" front 

LW=1.5
set style line 1 lw LW lc rgb 'blue'
set style line 2 lw LW lc rgb 'dark-grey' dashtype 2 
set style line 4 lw LW lt 1 lc rgb '#27ad81' dashtype 2 

set tics nomirror
set lmargin at screen 0.11 
set rmargin at screen 0.98
set xzeroaxis

set label 11 '(a)' at screen 0.025,0.98 center 
set label 12 '(b)' at screen 0.025,0.63 center 

set xr [0.:1.15]
set xtics (0.,0.2,0.4,0.6,0.8,1.0)

set multiplot

set key samplen 1. reverse Left
set label 1 '{/Times-Italic U(t)} (arb. units)' rotate by 90 at screen 0.06,0.98 right 
set yr [-0.15:1.1]
set ytics ('' 0.,'' 0.2,'' 0.4,'' 0.6,'' 0.8, '' 1.0)
set tmargin at screen 0.98
set bmargin at screen 0.65
set format x ''
p '../transducerSignal_inkOnglass.dat' u 1:2 w l ls 1 t '{/Times-Italic U}'\
, '../transducerSignal_inkOnGlass.dat' u 1:4 w l ls 4 t '{/Times-Italic U_{/Times-Roman=10 s}}'\

unset label 1
unset label 11 
unset label 12

unset key

set bmargin at screen 0.07
set tmargin at screen 0.63
set yr [4:7]
set ytics (4,5,5.5,6) #rotate by 20
set format y "%2.1f"
set format x "%2.1f"
set label 2 '{/Times-Italic t} ({/Symbol m}s)' at screen 0.98,0.03 right 
set label 3 '{/Times-Italic z} (mm)' rotate by 90 at screen 0.06,0.63 right 
set tics out 
set arrow 1 nohead lw 3 dashtype 4 from 0., 5.0 to 1.15, 5.0 front
set arrow 2 nohead lw 3 dashtype 3 from 0., 5.5 to 1.15,5.5 front
set arrow 3 nohead lw 3 dashtype 3 from 0., 5.55 to 1.15,5.55 front
set label 4 '{/Times-Italic p(z,t)}' at screen 0.15,0.61 front

unset colorbox 
set palette defined (0 'dark-pink', 0.5 'light-grey', 1 'blue')
set cbrange [-1:1]

p '../pzt.dat' nonuniform matrix u 2:1:3 with image

unset multiplot
