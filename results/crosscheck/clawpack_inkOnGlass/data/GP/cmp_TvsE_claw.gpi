set terminal postscript enhanced eps size 9.5cm,9cm color lw 1 16 font "Times-Roman"
set output './FIGS/cmp_FD_FV_EXP.eps'


LW=1.
set style line 1 lw LW lc rgb 'blue'
set style line 2 lw LW lc rgb 'black' dashtype 2 
set style line 3 lw LW lt 1 lc rgb "#FF8C00" dashtype 1

set label 106 "{/Times-Roman=70 DRAFT}" at screen 0.6,0.5 center rotate by 25 tc "grey" front 

set tics nomirror
set lmargin at screen 0.11 
set rmargin at screen 0.98
set xzeroaxis
set key samplen 1. reverse Left

set label 111 '(a)' at screen 0.025,0.97 center 
set label 112 '(b)' at screen 0.025,0.67 center 
set label 113 '(c)' at screen 0.025,0.37 center 

set xr [0.:2.]
set xtics 0.,0.5,1.8 

set yr [-0.6:1.1]
set ytics -1.2,0.4,1.2
set format y ''

set multiplot

set label 11 'FD code' rotate by 90 at screen 0.06,0.83 center 
set tmargin at screen 0.98
set bmargin at screen 0.68
set format x ''
p '../transducerSignal_inkOnGlass.dat' u 1:2 w l ls 1 t '{/Times-Italic U}'\
, '../transducerSignal_inkOnGlass.dat' u 1:3 w l ls 2 t '{/Times-Italic U_{/Times-Roman=10 LP}}'\


unset label 11
unset label 111 
unset label 112
unset label 113
unset label 106


set label 31 'HR FV code' rotate by 90 at screen 0.06,0.53 center 
set tmargin at screen 0.68
set bmargin at screen 0.38
set format x ''
p '../claw_transSig_inkOnGlass.dat' u 1:2 w l ls 1 t '{/Times-Italic U}'\
, '../claw_transSig_inkOnGlass.dat' u 1:3 w l ls 2 t '{/Times-Italic U_{/Times-Roman=10 LP}}'\

unset label 31

set label 21 'Measurement' rotate by 90 at screen 0.06,0.23 center
set tmargin at screen 0.38
set bmargin at screen 0.08
set format x '%2.1f'
fx1(x) = x*10**6 - 1.25
fx2(x) = x*10**6 - 1.135
fx(x) = x*10**6
fy1(y) = y/0.018
set label 22 '{/Times-Italic t} ({/Symbol m}s)' at screen 0.98,0.035 right 
p '../InkOnGlass.dat' every 8 u (fx1($1)):(fy1($3)) w l ls 3 t '{/Times-Italic U_{/Times-Roman=10 0}}'\
, '../InkOnGlass.dat' every 8 u (fx2($1)):($2) w l ls 2 t '{/Times-Italic U_{/Times-Roman=10 E}}'\



unset multiplot
